import java.nio.file.Files
import java.nio.file.Paths

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply from: "../download-libwallet.gradle"
apply plugin: "io.sentry.android.gradle"
apply plugin: "kotlin-parcelize"
apply plugin: "com.google.devtools.ksp"

android {
    namespace "com.tari.android.wallet"

    defaultConfig {
        applicationId "com.tari.android.wallet"
        minSdkVersion 26
        targetSdkVersion 34
        compileSdk 34
        versionCode buildNumber
        versionName versionNumber + "-libwallet-" + libwalletVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true

        ndk {
            abiFilters = []
            abiFilters.addAll(["arm64-v8a", "x86_64"])
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
            }
        }

        def yatProperties = loadYatProps()
        buildConfigField("String", "YAT_ORGANIZATION_NAME", "\"${yatProperties.getProperty("yat.name")}\"")
        buildConfigField("String", "YAT_ORGANIZATION_KEY", "\"${yatProperties.getProperty("yat.key")}\"")
        buildConfigField("String", "YAT_ORGANIZATION_RETURN_URL", "\"${yatProperties.getProperty("yat.returnUrl")}\"")
        def dropboxProperties = loadDropboxProps()
        buildConfigField("String", "DROPBOX_ACCESS_TOKEN", "\"${dropboxProperties.getProperty("dropbox_key")}\"")
        buildConfigField("String", "LIB_WALLET_MIN_VALID_VERSION", "\"$libwalletMinValidVersion\"")
    }

    flavorDimensions.add("privacy-mode")

    buildTypes {
        loadSentryProps()
        def secretProperties = loadSecretProps()

        debug {
            jniDebuggable true
            buildConfigField("String", "GIPHY_KEY", "\"${secretProperties.getProperty("giphy.key")}\"")
        }
        release {
            buildConfigField("String", "GIPHY_KEY", "\"${secretProperties.getProperty("giphy.key")}\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            ndk {
                debugSymbolLevel "FULL"
            }
            sentry {
                autoUpload.set(true)
                uploadNativeSymbols.set(true)
                includeNativeSources.set(true)
            }
        }
    }

    productFlavors {
        regular {
            dimension "privacy-mode"
            buildConfigField "String", "NOTIFICATIONS_API_KEY", "\"${loadSecretProps().getProperty("service.notifications.api_key")}\""
            proguardFile "regular-proguard-rules.pro"
        }
        privacy {
            dimension "privacy-mode"
            buildConfigField "String", "NOTIFICATIONS_API_KEY", "\"${loadSecretProps().getProperty("service.notifications.api_key")}\""
        }
    }

    applicationVariants.configureEach { variant ->
        variant.mergedFlavor.manifestPlaceholders.dropboxApiKey = loadDropboxProps().getProperty("dropbox_key")
        variant.mergedFlavor.manifestPlaceholders.sentryPublicDSN = loadSecretProps().getProperty("sentry.public_dsn")
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        aidl true
        buildConfig true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        resources {
            excludes += [
                    //added for exclude resources duplication
                    "META-INF/DEPENDENCIES",
                    "META-INF/LICENSE",
                    "META-INF/LICENSE.txt",
                    "META-INF/LICENSE.md",
                    "META-INF/license.txt",
                    "META-INF/NOTICE",
                    "META-INF/NOTICE.txt",
                    "META-INF/NOTICE.md",
                    "META-INF/notice.txt",
                    "META-INF/AL2.0",
                    "META-INF/LGPL2.1",
                    "META-INF/INDEX.LIST",
                    "META-INF/ASL2.0",
                    "META-INF/LICENSE-notice.md",
                    "META-INF/io.netty.versions.properties",
                    "mozilla/public-suffix-list.txt",
                    "xsd/catalog.xml",
                    "com/itextpdf/io/font/cmap/HYSMyeongJoStd-Medium.properties",
                    "com/itextpdf/io/font/cmap/HYGoThic-Medium.properties",
                    "com/itextpdf/io/font/cmap/HYSMyeongJo-Medium.properties",
                    "com/itextpdf/io/font/cmap/KozMinPro-Regular.properties",
                    "com/itextpdf/io/font/cmap/MSungStd-Light.properties",
                    "com/itextpdf/io/font/cmap_info.txt",
                    "com/itextpdf/io/font/cmap/STSong-Light.properties",
                    "com/itextpdf/io/font/cmap/STSongStd-Light.properties",
                    "com/itextpdf/io/font/cmap/HeiseiKakuGo-W5.properties",
                    "com/itextpdf/io/font/cmap/MSung-Light.properties",
                    "com/itextpdf/io/font/cmap/MHei-Medium.properties",
                    "com/itextpdf/io/font/cmap/HeiseiMin-W3.properties",
                    "com/itextpdf/io/font/cmap/cjk_registry.properties"
            ]
        }
    }
    lint {
        disable "LintError", "NewApi", "TimberExceptionLogging", "LogNotTimber", "StringFormatInTimber",
                "ThrowableNotAtBeginning", "BinaryOperationInTimber", "TimberArgCount", "TimberArgTypes",
                "TimberTagLength"
    }

    configurations.configureEach {
        resolutionStrategy.dependencySubstitution {
            // Substitute one module dependency for another for excluding libs duplication
            substitute module("com.sun.activation:javax.activation:1.2.0") using module("jakarta.activation:jakarta.activation-api:1.2.1")
            substitute module("org.jetbrains.kotlin:kotlin-android-extensions-runtime:1.4.20") using module("org.jetbrains.kotlin:kotlin-android-extensions:1.6.21")
            substitute module("xpp3:xpp3:1.1.4c") using module("xml-apis:xml-apis:1.4.01")
            substitute module("xmlpull:xmlpull:1.1.3.1") using module("net.sf.kxml:kxml2:2.3.0")
            substitute module("org.jetbrains.kotlin:kotlin-build-common:1.6.21") using module("org.jetbrains.kotlin:kotlin-compiler-embeddable:1.6.21")
            substitute module("org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21") using module("org.jetbrains.kotlin:kotlin-compiler-embeddable:1.6.21")
        }
    }
}

Properties loadSecretProps() {
    return loadProps("secret")
}

Properties loadSentryProps() {
    return loadProps("sentry")
}

Properties loadYatProps() {
    return loadProps("yat")
}

Properties loadDropboxProps() {
    return loadProps("dropbox")
}

Properties loadProps(fileName) {
    final File props = project.rootProject.file(fileName + ".properties")
    if (!props.exists()) {
        Files.copy(
                Paths.get(project.rootProject.file(fileName + "-example.properties").absolutePath),
                Paths.get(props.absolutePath),
        )
    }
    final Properties properties = new Properties()
    properties.load(props.newDataInputStream())
    return properties
}

// Comment this line if you want to disable the automatic download and use the native libraries of your choice
preBuild.dependsOn("downloadLibwallet")

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutines_version"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.6.2"

    // kotlin extensions
    implementation "androidx.core:core-ktx:1.12.0"
    // android
    implementation "androidx.appcompat:appcompat:1.6.1"
    // support lib
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    // android jetpack
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    // recycler view
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    // the new view pager
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    // for tab layout
    implementation "com.google.android.material:material:1.11.0"
    // lottie
    implementation 'com.airbnb.android:lottie:6.1.0'
    // Glide - image processing & caching library
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    ksp 'com.github.bumptech.glide:compiler:4.16.0'

    // biometric
    implementation "androidx.biometric:biometric:1.1.0"

    // joda
    implementation 'net.danlew:android.joda:2.12.5'

    // parceler
    implementation "org.parceler:parceler-api:1.1.13"
    ksp "org.parceler:parceler:1.1.13"

    // dagger - DI
    implementation 'com.google.dagger:dagger:2.48.1'
    ksp 'com.google.dagger:dagger-compiler:2.48.1'

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"

    // logger
    implementation "com.orhanobut:logger:2.2.0"

    // QR scanner
    implementation "com.github.yuriy-budiyev:code-scanner:2.3.2"
    implementation "com.journeyapps:zxing-android-embedded:4.3.0"

    // encryption
    implementation "com.github.adorsys:secure-storage-android:0.0.2"

    // Tor control
    implementation "info.guardianproject:tor-android:0.4.8.7"
    implementation "info.guardianproject:jtorctl:0.4.5.7"

    // used to read log files
    //noinspection GradleDependency
    implementation "commons-io:commons-io:2.11.0"

    // JSON reflection-based (+de)serialization library
    implementation 'com.google.code.gson:gson:2.10.1'

    // Retrofit2 - Popular (2020) REST API wrapper with Java interface
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11"

    // Google services & Google drive
    implementation platform('com.google.firebase:firebase-bom:32.7.3')
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation 'com.google.android.gms:play-services-auth:21.0.0'
    implementation 'com.google.http-client:google-http-client-gson:1.43.3'
    implementation('com.google.api-client:google-api-client-android:2.2.0') {
        exclude group: "org.apache.httpcomponents"
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev20230822-2.0.0') {
        exclude group: "org.apache.httpcomponents"
    }
    // sentry - crash analytics
    implementation 'io.sentry:sentry-android:6.33.1'

    implementation "com.dropbox.core:dropbox-core-sdk:5.4.6"

    // It's recommended to use the latest version of the library from the JitPack repository,
    // but you can also use a locally build version of the library by using the ":yatlib" module and `yat-lib-debug-snapshot.aar` file with your build
    implementation "com.github.tari-project:yat-lib-android:0.5.0"
//     implementation project(":yatlib")

    // custom libraries region
    // flex layout
    implementation "com.google.android.flexbox:flexbox:3.0.0"
    // overscroll
    implementation "io.github.everythingme:overscroll-decor-android:1.1.1"

    // giphy, don"t update to 2.2.0 because of not compatible yet
    implementation 'com.giphy.sdk:ui:2.3.13'
    // spring animation
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "com.github.MasayukiSuda:EasingInterpolator:v1.3.2"

    // seismic - device shake detector
    implementation "com.squareup:seismic:1.0.3"

    implementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.3.0"

    implementation 'com.itextpdf:itext7-core:8.0.2'

    implementation("org.apache.maven:maven-artifact:3.9.5")

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'

    implementation 'com.github.vestrel00:contacts-android:0.3.1'

    implementation "com.github.weliem:blessed-android:2.5.0"

    // test
    testImplementation "junit:junit:4.13.2"
    testImplementation 'io.mockk:mockk:1.13.8'
    androidTestImplementation 'io.mockk:mockk-android:1.13.8'
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}
